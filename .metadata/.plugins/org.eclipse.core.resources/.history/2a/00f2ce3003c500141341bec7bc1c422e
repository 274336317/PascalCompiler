/*
 * generated by Xtext
 */
package org.xtext.example.pascal.generator

import java.util.HashMap
import java.util.Map
import java.util.Set
import org.eclipse.emf.ecore.EObject
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IFileSystemAccess
import org.eclipse.xtext.generator.IGenerator
import org.xtext.example.pascal.pascal.block
import org.xtext.example.pascal.pascal.expression
import org.xtext.example.pascal.pascal.factor
import org.xtext.example.pascal.pascal.function_designator
import org.xtext.example.pascal.pascal.program
import org.xtext.example.pascal.pascal.statement_part
import org.xtext.example.pascal.pascal.term
import org.xtext.example.pascal.validation.ComposedType
import org.xtext.example.pascal.validation.ComposedTypeKind
import org.xtext.example.pascal.validation.ElementType
import org.xtext.example.pascal.validation.PascalValidator
import org.xtext.example.pascal.validation.Type
import org.xtext.example.pascal.validation.TypeInferer
import org.xtext.example.pascal.validation.Variable

/**
 * Generates code from your model files on save.
 * 
 * see http://www.eclipse.org/Xtext/documentation.html#TutorialCodeGeneration
 */
class PascalGenerator implements IGenerator {
	
	private HashMap<String, String> stringTable = new HashMap<String, String>();
	
	override void doGenerate(Resource resource, IFileSystemAccess fsa) {
		for (e: resource.allContents.toIterable.filter(program)) {
			fsa.generateFile(e.heading.name + ".asm", e.compile);
		}
	}

	def createStringTable(program e) {
		stringTable.clear();
		for (s : e.eAllContents.toIterable.filter(factor)) {
			if (s.string != null) {
				if (!stringTable.containsKey(s.string)) {
					stringTable.put(s.string, "__STRING_" + stringTable.size());
				}
			}
		}
	}
	
	def getVariables(program e, block b) {
		var artefacts = PascalValidator.artefacts.get(e.heading.name);
		var map = artefacts.get("variables") as Map<block, Set<Variable>>;
		//memoryCount.put(b, memoryInit);
		createStringTable(e);
		return map.get(b);
	}
	
	def intToHex(int x) {
		return String.format("0x%x", x);
	}
	
	def getNumberOfBytes(Variable v) {
		var type = v.varType.realType.toLowerCase;
		if (type.equals("boolean") || type.equals("shortint")) {
			return 1;
		} else if (type.equals("char")) {
			return 2;
		} else if (type.equals("integer")) {
			return 4;
		} else if (type.equals("longint") || type.equals("real")) {
			return 8;
		} 
		return 8;
	}
	
	def getValue(Variable v) {
		if (v.value instanceof String) {
			return "'" + v.value.toString.replaceAll("'", "\'") + "'";
		} else if (v.value instanceof Boolean) {
			if (v.value.equals(true)) {
				return 1;
			} else {
				return 0;
			}
		}
		return v.value;
	}
	
	def Type getType(program e, expression expr) {
		var artefacts = PascalValidator.artefacts.get(e.heading.name);
		var map = artefacts.get("calculatedTypes") as Map<EObject, Type>;
		return map.get(expr); 
	}
	
	def compile(program e) '''
		; Program «e.heading.name»
		global _main
		
		extern _printf
		extern _scanf 
		
		«e.compile(e.block, e.getVariables(e.block))» 
		
		; Code
		section .text
		_main:
		«e.compile(e.block, e.block.statement)» 
		RET	; Exit program
	'''

	def compile(program e, block b, Set<Variable> variables) ''' 
		; Loading global constants and strings
		section .data
		__NEW_LINE db 10
		__PRINTF_S db '%s'
		__PRINTF_I db '%d'
		__PRINTF_F db '%f'
		«FOR s : stringTable.keySet» 
			«e.compileGlobalConstant(new Variable(stringTable.get(s), new ComposedType(new Type("char"), 
				ComposedTypeKind.ARRAY), false, ElementType.CONSTANT, s), b)»
		«ENDFOR»
		«FOR v : variables»
			«e.compileGlobalConstant(v, b)»
		«ENDFOR»
		
		; Loading global variables
		section .bss
		«FOR v : variables»
			«e.compileGlobalVariables(v, b)»
		«ENDFOR» 
	'''
	 
	def compileGlobalConstant(program e, Variable v, block b) '''
		«IF v.type == ElementType.CONSTANT»
			«IF v.varType.realType.toLowerCase.equals("array of char")» 
				«v.name» db «getValue(v)»
			«ELSE»
				«v.name» equ «getValue(v)»
			«ENDIF»
		«ENDIF»
	'''
	
	def compileGlobalVariables(program e, Variable v, block b) '''
		«IF v.type == ElementType.VARIABLE»
			«v.name» RESB «getNumberOfBytes(v)»
		«ENDIF»
	'''
	
	def computeFactor(program e, factor f, String dstRegister) '''
		«IF f.string != null»
			mov «dstRegister», «stringTable.get(f.string)»
		«ENDIF»
	'''
	
	def computeTerm(program e, term t, String dstRegister) '''
		«FOR f : t.factors»
			«e.computeFactor(f, dstRegister)»
		«ENDFOR»
	'''
	
	def computeExpression(program e, expression exp, String dstRegister) '''
		«FOR expr : exp.expressions»
			«FOR t : expr.terms»
				«e.computeTerm(t, dstRegister)»
			«ENDFOR»
		«ENDFOR»
	'''
	
	def getPrintfDataType(Type dataType) '''
		«IF TypeInferer.getTypeWeight(dataType) == 4»
			__PRINTF_F
		«ELSEIF TypeInferer.getTypeWeight(dataType) >= 0»
			__PRINTF_I
		«ELSE»
			__PRINTF_S
		«ENDIF»
	'''
	
	def print(program e, String register, Type dataType) '''
		; Print
		sub esp, «getNumberOfBytes(new Variable("var", dataType, false, ElementType.VARIABLE)»
		mov [esp], «register»
		sub esp, 8
		mov [esp], «getPrintfDataType(dataType)»
		call _printf
		add esp, 4
		add esp, 8
		
	'''
	
	def print(program e, function_designator function) '''
		«IF function.expressions == null»
			«print(e, "__NEW_LINE", new Type("array of char"))»
		«ELSE»
			«e.computeExpression(function.expressions.expressions.get(0), "eax")»
			«print(e, "eax", e.getType(function.expressions.expressions.get(0)))»
		«ENDIF»
	'''
	
	def compile(program e, block b, statement_part part) '''
		«FOR s : part.sequence.statements»
			«IF s.simple != null»
				«IF s.simple.function_noargs != null»
					«IF s.simple.function_noargs.equals("writeln")»
						«print(e, "__NEW_LINE", new Type("array of char"))»
					«ENDIF»
				«ELSEIF s.simple.function != null»
					«IF s.simple.function.name.equals("write")»
						«print(e, s.simple.function)»
					«ELSEIF s.simple.function.name.equals("writeln")»
						«print(e, s.simple.function)»
						«print(e, "__NEW_LINE", new Type("array of char"))» 
					«ENDIF»
				«ENDIF»
			«ENDIF»
		«ENDFOR» 
	'''
	
}
